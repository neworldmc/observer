import net.fabricmc.loom.task.RemapJarTask

plugins {
    id 'fabric-loom' version '0.4-SNAPSHOT'
    id 'maven-publish'
    id 'org.jetbrains.kotlin.jvm' version "1.3.72"
    id 'com.github.johnrengelman.shadow' version '5.0.0'
}

sourceCompatibility = JavaVersion.VERSION_11
targetCompatibility = JavaVersion.VERSION_11

archivesBaseName = project.archives_base_name
version = project.mod_version
group = project.maven_group

repositories {
    mavenCentral()
    jcenter()
    maven { url "http://nexus.newinfinideas.cn/repository/maven-public" }
}

dependencies {
    // Related to fabric modding environment
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"
    modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"
    modImplementation "net.fabricmc:fabric-language-kotlin:${project.fabric_kotlin_version}"
    // Related to the actual implementation
    modImplementation 'site.neworld.objective:core:0.0.1'
    modImplementation 'site.neworld.objective:embeded:0.0.1'
    shadow 'site.neworld.objective:core:0.0.1'
    shadow 'site.neworld.objective:embeded:0.0.1'
}

processResources {
    inputs.property "version", project.version

    from(sourceSets.main.resources.srcDirs) {
        include "fabric.mod.json"
        expand "version": project.version
    }

    from(sourceSets.main.resources.srcDirs) {exclude "fabric.mod.json"}
}

tasks.withType(JavaCompile) { options.encoding = "UTF-8" }

task sourcesJar(type: Jar, dependsOn: classes) {
    archiveClassifier.set("sources")
    from sourceSets.main.allSource
}

shadowJar {
    minimize()
    archiveClassifier.set("all-dev")
    configurations = [project.configurations.shadow]
    dependencies {
        exclude(dependency('org.jetbrains.kotlin::.*')) // already have that in env
        exclude(dependency('org.jetbrains.kotlinx::.*')) // already have that in env
        exclude(dependency('it.unimi.dsi:fastutil:.*')) // we have that in minecraft. removed
    }
}

task remapShadow(type: RemapJarTask, dependsOn: shadowJar) {
    archiveClassifier.set("all")
    setInput(shadowJar.archiveFile)
    remapAccessWidener.set(remapJar.remapAccessWidener)
    addNestedDependencies.set(remapJar.addNestedDependencies)
}

assemble {
    configure {
        dependsOn.add(remapShadow)
    }
}

// configure the maven publication
publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact(remapJar) { builtBy remapJar }
            artifact(sourcesJar) { builtBy remapSourcesJar }
        }
    }

    repositories {
    }
}
